// SPDX-License-Identifier: MIT
pragma solidity ^0.8.20;

import {AMMPair} from "./AMMPair.sol";

contract AMMFactory {
    address public immutable protocolFees;
    mapping(address => mapping(address => address)) public getPair;
    address[] public allPairs;

    event PairCreated(address indexed token0, address indexed token1, address pair, uint256 index);

    constructor(address protocolFees_) {
        protocolFees = protocolFees_;
    }

    function allPairsLength() external view returns (uint256) {
        return allPairs.length;
    }

    function createPair(address tokenA, address tokenB) external returns (address pair) {
        require(tokenA != tokenB, "Factory: identical");
        (address token0, address token1) = tokenA < tokenB ? (tokenA, tokenB) : (tokenB, tokenA);
        require(token0 != address(0), "Factory: zero");
        require(getPair[token0][token1] == address(0), "Factory: exists");

        AMMPair p = new AMMPair(token0, token1, protocolFees);
        pair = address(p);

        getPair[token0][token1] = pair;
        getPair[token1][token0] = pair;
        allPairs.push(pair);

        emit PairCreated(token0, token1, pair, allPairs.length - 1);
    }
}
